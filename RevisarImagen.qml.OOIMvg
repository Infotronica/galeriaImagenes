import QtQuick 2.9
import QtQuick.Controls 2.2
import QtQuick.Layouts 1.3

Item {
    id: itemContainer
    Layout.fillWidth: true
    Layout.fillHeight: true
    property string sourceImage: ""

    Flickable {
        id: flick
        anchors.fill: parent
        //contentWidth: imgPreview.sourceSize.width
        //contentHeight: imgPreview.sourceSize.height
        contentWidth: anchoPantalla
        contentHeight: altoPantalla

        PinchArea {
            width: Math.max(flick.contentWidth, flick.width)
            height: Math.max(flick.contentHeight, flick.height)

            property real initialWidth
            property real initialHeight
            //![0]
            onPinchStarted: {
                initialWidth = flick.contentWidth
                initialHeight = flick.contentHeight
            }

            onPinchUpdated: {
                // adjust content pos due to drag
                flick.contentX += pinch.previousCenter.x - pinch.center.x
                flick.contentY += pinch.previousCenter.y - pinch.center.y

                // resize content
                flick.resizeContent(initialWidth * pinch.scale, initialHeight * pinch.scale, pinch.center)
            }

            onPinchFinished: {
                // Move its content within bounds.
                flick.returnToBounds()
            }
            //![0]

            Rectangle {
                width: flick.contentWidth
                height: flick.contentHeight
                color: "white"

                Image {
                    id: imgPreview
                    anchors.fill: parent
                    source: sourceImage
                    fillMode: Image.PreserveAspectFit

                    MouseArea {
                        anchors.fill: parent
                        onDoubleClicked: {
                            flick.contentWidth = anchoPantalla
                            flick.contentHeight = altoPantalla
                        }
                    }

                    onStatusChanged: {
                        rectVelo.visible=false
                    }
                }
            }
        }
    }
}
